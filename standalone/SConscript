# -*- Mode: Python -*-

freestanding_env = Environment()

freestanding_env['CFLAGS']  = "-Os -minline-all-stringops -m32 -march=pentium -mtune=core2 -pipe -std=gnu99 -ffreestanding -nostdlib -Wno-multichar -Wall"
freestanding_env['CFLAGS'] += " -ffunction-sections -fomit-frame-pointer -mregparm=3 "
freestanding_env['LINKFLAGS'] = "-m elf_i386 -gc-sections -N"
freestanding_env['LINK'] = "ld"
freestanding_env['ASFLAGS'] = "-m32"

conf = Configure(freestanding_env)

if not (conf.CheckCHeader("stdint.h") and
        conf.CheckCHeader("stdarg.h")):
    print("Standard C headers are missing.")
    Exit(1)

freestanding_env = conf.Finish()

fenv = freestanding_env.Clone()

fenv['CPPPATH'] = ["include/"]
fenv['LINKFLAGS'] += " -T " + File('morbo.ld').srcnode().path
fenv['LIBPATH'] = ['.']

stand = fenv.StaticLibrary('stand',
                           [ 'elf.c',
                             'mbi.c',
                             'pci.c',
                             'pci_db.c',
                             'printf.c',
                             'reboot.c',
                             'serial.c',
                             'start.S',
                             'util.c',
                             'version.c',

                             # libc stuff
                             'memcpy.c',
                             'memcmp.c',
                             'memset.c',
                             'strlen.c',

                             # FreeBSD libc stuff
                             'strncmp.c',
                             'strncpy.c',
                             'strtok.c',
                             'strtoull.c',
                             'qdivrem.c',
                             ])

tinf = fenv.StaticLibrary('tinf',
                          [ 'tinflate.c',
                            'tinfgzip.c',
                            'tinfzlib.c',
                            'adler32.c',
                            'crc32.c',
                            ])

# Execute git describe somewhere where our code is. This is useful,
# when this SConscript is used by third-party scons build systems
# directly.
AlwaysBuild(Command('version.inc', ['include/version.h'], """( cd `dirname $SOURCE` && git describe --dirty || echo UNKNOWN ) | sed 's/^\\(.*\\)$/"\\1"/' > $TARGET"""))

def DoInstall(prog):
    Depends(prog, 'morbo.ld')
    Install('../tftp', prog)

# Morbo

DoInstall(fenv.Program('bender',
                       [ 'bender.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Performance tests

DoInstall(fenv.Program('basicperf',
                       [ 'basicperf.c' ],
                       LIBS=['stand']))

# EOF
